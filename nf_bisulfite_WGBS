#!/usr/bin/env nextflow

// Enable modules
nextflow.preview.dsl=2

params.outdir = "."
params.genome = ""
params.verbose = false
params.single_end = false  // default mode is auto-detect. NOTE: params are automatically handed over 

params.fastqc_args = ''
params.fastq_screen_args = ''
params.trim_galore_args = ''
params.bismark_args = ''
params.deduplicate_bismark_args = ''
params.bismark_methylation_extractor_args = ''
params.bismark2summary_args = ''
params.bismark2report_args= ''
params.multiqc_args = ''

if (params.verbose){
    println ("[WORKFLOW] FASTQC ARGS: "           + params.fastqc_args)
    println ("[WORKFLOW] FASTQ SCREEN ARGS ARE: " + params.fastq_screen_args)
    println ("[WORKFLOW] TRIM GALORE ARGS: "      + params.trim_galore_args)
    println ("[WORKFLOW] BISMARK ARGS ARE: "      + params.bismark_args)
    println ("[WORKFLOW] BISMARK DEDUPLICATION ARGS ARE: "         + params.deduplicate_bismark_args)
    println ("[WORKFLOW] BISMARK METHYLATION EXTRACTOR ARGS ARE: " + params.bismark_methylation_extractor_args)
    println ("[WORKFLOW] MULTIQC ARGS: "          + params.multiqc_args)
}

include { makeFilesChannel; getFileBaseNames } from './nf_modules/files.mod.nf'
include getGenome                              from './nf_modules/genomes.mod.nf'
genome = getGenome(params.genome)

include FASTQC                                 from './nf_modules/fastqc.mod.nf'                         
include FASTQC as FASTQC2                      from './nf_modules/fastqc.mod.nf'

include FASTQ_SCREEN                           from './nf_modules/fastq_screen.mod.nf'   params(bisulfite: '--bisulfite')
include TRIM_GALORE                            from './nf_modules/trim_galore.mod.nf'                    

include BISMARK                                from './nf_modules/bismark.mod.nf'        params(genome: genome)
include BISMARK_DEDUPLICATION                  from './nf_modules/bismark_deduplication.mod.nf'
include BISMARK_METHYLATION_EXTRACTOR          from './nf_modules/bismark_methylation_extractor.mod.nf' 
include BISMARK2REPORT                         from './nf_modules/bismark2report.mod.nf'
include BISMARK2SUMMARY                        from './nf_modules/bismark2summary.mod.nf'

include MULTIQC                                from './nf_modules/multiqc.mod.nf' 

file_ch = makeFilesChannel(args)

workflow {

    main:
        FASTQC                 (file_ch, params.outdir, params.fastqc_args, params.verbose)
        TRIM_GALORE            (file_ch, params.outdir, params.trim_galore_args, params.verbose)
        FASTQ_SCREEN           (TRIM_GALORE.out.reads, params.outdir, params.fastq_screen_args, params.verbose)
        FASTQC2                (TRIM_GALORE.out.reads, params.outdir, params.fastqc_args, params.verbose)
        BISMARK                (TRIM_GALORE.out.reads, params.outdir, params.bismark_args, params.verbose)
        BISMARK_DEDUPLICATION  (BISMARK.out.bam, params.outdir, params.deduplicate_bismark_args, params.verbose)
        BISMARK_METHYLATION_EXTRACTOR(BISMARK_DEDUPLICATION.out.bam, params.outdir, params.bismark_methylation_extractor_args, params.verbose)

        // merging channels for MultiQC
        multiqc_ch = FASTQC.out.report.mix(
            TRIM_GALORE.out.report,
            FASTQ_SCREEN.out.report.ifEmpty([]),
            FASTQC2.out.report.ifEmpty([]),
            BISMARK.out.report.ifEmpty([]),
            BISMARK_DEDUPLICATION.out.report.ifEmpty([]),
            BISMARK_METHYLATION_EXTRACTOR.out.report.ifEmpty([]),
            BISMARK_METHYLATION_EXTRACTOR.out.mbias.ifEmpty([]),
        ).collect()
        // merging channels for Bismark reports
        bismark_report_ch = BISMARK.out.bam.mix(
            BISMARK.out.report,
            BISMARK_DEDUPLICATION.out.report.ifEmpty([]),
            BISMARK_METHYLATION_EXTRACTOR.out.report.ifEmpty([]),
            BISMARK_METHYLATION_EXTRACTOR.out.mbias.ifEmpty([]),
        ).collect()

        // bismark_report_ch.subscribe {  println "Got: $it"  }

        BISMARK2REPORT                   (bismark_report_ch, params.outdir, params.bismark2report_args, params.verbose)
        BISMARK2SUMMARY                  (bismark_report_ch, params.outdir, params.bismark2summary_args, params.verbose)
        MULTIQC                          (multiqc_ch, params.outdir, params.multiqc_args, params.verbose)
   }

